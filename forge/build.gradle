plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "better_slots.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    // Dependencies
    modCompileOnly("me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}")
    modRuntimeOnly("me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}")

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    def replaceProperties = new HashMap<String, String>()

    project.properties.each { key, value ->
        if (value instanceof String) {
            replaceProperties.put(key, value)
        }
    }

    inputs.properties replaceProperties

    filesMatching('META-INF/mods.toml') {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
